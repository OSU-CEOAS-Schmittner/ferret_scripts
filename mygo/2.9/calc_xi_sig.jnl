! calculates streamfunction for UVic model on density levels
! arguments: $1: time averaging period e.g. l=@sbx:12
define symbol tav = $1%l=@ave%
let sigma=rho_un(o_sal,o_temp,0)-1000
let vvz = o_vely[($tav)]
def axis/z=22:28.5:0.01/depth axden

!file/var=slev0/skip=2 "/home/aschmitt/local/ferret/mygo/2.9/sigma_layers.dat"
!def axis/z/from_data/depth/name=axden slev0[d=2]

let sigv = sigma[g=vvz]

!let v_sig = ZAXREPLACE( vvz, sigv[x=@ave], z[gz=axsig])

let vv = o_vely[($tav)]

let v_xdin = vv[x=@din] 
let va = if G_MSKHR[g=vv] eq 1 then vv 
let vp = if G_MSKHR[g=vv] eq 2 then vv
let vi = if G_MSKHR[g=vv] eq 3 then vv
let vpi = if ((G_MSKHR[g=vv] eq 2) or (G_MSKHR[g=vv] eq 3)) then vv

let siga = if G_MSKHR[g=vv] eq 1 then sigv
let sigp = if G_MSKHR[g=vv] eq 2 then sigv
let sigi = if G_MSKHR[g=vv] eq 3 then sigv
let sigpi = if ((G_MSKHR[g=vv] eq 2) or (G_MSKHR[g=vv] eq 3)) then sigv

let va_xdin = va[x=@din] 
let vp_xdin = vp[x=@din]
let vi_xdin = vi[x=@din]
let vpi_xdin = vpi[x=@din]
let vs_xdin = if y lt 35 then vv[x=@din]

let/title="Streamfunction Atlantic"/units="Sv" xi_a = (va_xdin[z=@iin]-va_xdin[z=@din])*1e-6
let/title="Streamfunction Pacific"/units="Sv" xi_p = (vp_xdin[z=@iin]-vp_xdin[z=@din])*1e-6
let/title="Streamfunction Indic"/units="Sv" xi_i = (vi_xdin[z=@iin]-vi_xdin[z=@din])*1e-6
let/title="Streamfunction Pacific, Indian"/units="Sv" xi_pi = (vpi_xdin[z=@iin]-vpi_xdin[z=@din])*1e-6
let/title="Streamfunction ACC"/units="Sv" xi_s = (vs_xdin[z=@iin]-vs_xdin[z=@din])*1e-6
let/title="Global Streamfunction"/units="Sv" xi = (v_xdin[z=@iin]-v_xdin[z=@din])*1e-6
let xi_zst = xi_a[gz=depth_stag]
let xi_test = if (k eq 1) then 0 else xi_zst ! sets xi = 0 at surface

let xi_sig = ZAXREPLACE( xi, sigv[x=@ave], z[gz=axden])
let xi_sig_a = ZAXREPLACE( xi_a, siga[x=@ave], z[gz=axden])
let xi_sig_p = ZAXREPLACE( xi_p, sigp[x=@ave], z[gz=axden])
let xi_sig_i = ZAXREPLACE( xi_i, sigi[x=@ave], z[gz=axden])
let xi_sig_pi = ZAXREPLACE( xi_pi, sigpi[x=@ave], z[gz=axden])

let/units=Sv/title="Atlantic Overturning at 25N" amoc25 = xi_a[z=300:5000@max,y=25]
let/units=Sv/title="Atlantic Overturning at 32S" amoc32s = xi_a[z=300:5000@max,y=32s]
let/units=Sv/title="Maximum Atlantic Overturning"  ov=xi_a[z=300:5000@max,y=0:90@max]
let/units=Sv/title="Indonesian Throughflow"  ITF=o_velx[x=119e,y=20s:2s@din,z=0:6000@din]*(-1e-6)
let/units=Sv/title="Antarctic Circumpolar Current" ACC = o_velx[x=68w,y=70s:50s@din,z=0:6000@din]*1e-6
!let/units=Sv/title="Southern Ocean Upwelling" SOU = xi_tot[y=90s:35s@max,z=300:5000@max]
let/units=Sv/title="Antarctic Bottom Water" AABW = xi[y=35s@min,z=1000:7000@min]
let/units=Sv/title="Antarctic Bottom Water Flow Into Atlantic" AABWA = xi_a[y=35s@min,z=1000:7000@min]
let/units=Sv/title="Circumpolar Deep Water Flow Into Pacific/Indian at 35S" CPDWPI35s = xi_pi[y=35s@min,z=1000:7000@min]
let/units=Sv/title="Circumpolar Deep Water Flow Into Pacific/Indian at 20S" CPDWPI20s = xi_pi[y=20s@min,z=1000:7000@min]
let/units=Sv/title="Circumpolar Deep Water Flow Into Pacific/Indian at Equator" CPDWPIEQ = xi_pi[y=0s@min,z=1000:7000@min]
let/units=Sv/title="Circumpolar Deep Water Flow Into Indian at 20S" cpdwi20s = XI_i[y=20s@min,z=1000:7000@min]
let/units=Sv/title="Circumpolar Deep Water Flow Into Indian at 32S" cpdwi32s = XI_i[y=32s@min,z=1000:7000@min]
let/units=Sv/title="Circumpolar Deep Water Flow Into Pacific at 32S" cpdwp32s = XI_p[y=32s@min,z=1000:7000@min]

let/units=Sv/title="Global Upper Cell (NADW)" gl_md = xi[y=0:90@max,z=400:7000@max]
let/units=Sv/title="Global Upper Cell (AABW)" gl_ld = xi[y=90s:90n@min,z=1500:7000@min]

