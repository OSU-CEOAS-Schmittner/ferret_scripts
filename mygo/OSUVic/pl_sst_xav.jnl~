!***********************************************************************
! Description: plots sea surface temperature
! PUMA versus WOA05
!
! use: go pl_sst 1 l=1:12@ave "My Model"
!
! arguments  : $1 : data set e.g. 1 or model.output.nc
!            : $2 : time (averaging) period e.g. l=1:12@ave or l=2
!            : $3 : model acromym (optional)
!
!***********************************************************************

def symbol dataset = $1%1%
def symbol tim = $2%l=1%
def sym model = $3%MODEL%
def sym lev = lev=(-2,34,2)
!go portraitNxN 2 2 95 14
go portraitNxN 2 3 95 30
!set win/asp=0.9
set reg/x=180:540
!set axis/modulo xt
ppl axlsze .14,.14
ppl labset .18,.16,.16,.16
ppl shakey ,,.12,,,,,,
pal inverse_greyscale
pal default

use "/home/aschmitt/data/woa05/t00an1.nc"
def grid/like=temperature[d=($dataset)]/t=t00an1 mg
let tobs = t00an1[g=mg@ave,d=t00an1.nc]

set view 1
shade/set/nolab/($lev) tobs[k=1]
ppl shakey 1,1,0.16
ppl shade
fill/nolab/o/($lev) tobs[k=1]
go land
label/nouser `($ppl$xlen)/2` `($ppl$ylen)*1.1` 0,0,0.2 "SST"
label/nouser `($ppl$xlen)*0.75` `($ppl$ylen)*0.85` 0,0,0.2 "WOA05"


set view 3
let sst = temperature[($2),d=($dataset),k=1]-273.15
shade/set/nolab/($lev) sst
ppl shakey 1,1,0.16
ppl shade
fill/nolab/o/($lev) sst
go land
label/nouser `($ppl$xlen)*0.7` `($ppl$ylen)*0.8` -1,0,0.2 ($model)

set view 5
let dsst = sst - tobs[k=1,l=1]
shade/set/nolab/lev=(-10,10,2)/pal=light_centered dsst
ppl shakey 1,1,0.16
ppl shade
fill/nolab/o/lev=(-10,10,2)/pal=light_centered dsst
go land
label/nouser `($ppl$xlen)*0.7` `($ppl$ylen)*0.8` -1,0,0.2 ($model)-OBS
