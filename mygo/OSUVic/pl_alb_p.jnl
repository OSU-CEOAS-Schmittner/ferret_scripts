!***********************************************************************
! Description: plots top-of-atmosphere net radiation for 
! PUMA versus ERBE observations
!
! use: go pl_toanet 1 l=1:12@ave "My Model"
!
! arguments  : $1 : data set e.g. 1 or model.output.nc
!            : $2 : time (averaging) period e.g. l=1:12@ave or l=2
!            : $3 : model acromym (optional)
!
!***********************************************************************

def symbol dataset = $1%1%
def symbol tim = $2%l=1%
def sym model = $3%MODEL%
def sym lev = lev=(0,1,.1)
def sym lev2 = lev=(-inf)(-.3,.3,.05)(inf)
go portraitNxN 2 2 5 20
set win/asp=0.8
set reg/x=180:540
!set axis/modulo xt
ppl labset .18,.16,.16,.16
ppl shakey ,,.12,,,,,,
!pal centered
pal default
let sol = tsr-tsru
let albm = (-1)*tsru/sol
let mod = albm[($2),d=($dataset)]
def grid/like=tsr[d=($dataset)] mg

use "/home/aschmitt/data/erbe/ERBE_mean.cdf"
let alb = refl/solar
let obs = alb[g=mg@ave]

set view 1
shade/set/nolab/($lev) obs
ppl axlsze 0,.14
ppl shakey 1,1,0.16
ppl shade
fill/nolab/o/($lev) obs
con/nolab/o/($lev) obs
go land
label/nouser `($ppl$xlen)*1.2` `($ppl$ylen)*1.1` 0,0,0.2 "Planetary Albedo"
label/nouser `($ppl$xlen)*0.98` `($ppl$ylen)*0.95` 1,0,0.16 "ERBE (1985-1989)"

set view 2
shade/set/nolab/($lev)/nokey mod
ppl axlsze .14,0
ppl shade
fill/nolab/o/($lev) mod
con/nolab/o/($lev) mod
go land
label/nouser `($ppl$xlen)*0.98` `($ppl$ylen)*0.95` 1,0,0.16 ($model)

set view 3
let delta = mod - obs
shade/set/nolab/($lev2)/pal=light_centered delta
ppl shakey 1,1,0.16
ppl axlsze .14,.14
ppl shade
fill/nolab/o/($lev2)/pal=light_centered delta
go land
label/nouser `($ppl$xlen)*1.` `($ppl$ylen)*0.95` 1,0,0.15 Model-ERBE

set view 4
pl/nolab/set/axes=0,1,0,1 obs[x=@ave], mod[x=@ave]
ppl yfor (f3.1)
ppl plot
label/nouser `($ppl$xlen)*1.` `($ppl$ylen)*0.95` 1,0,0.15 ERBE
label/nouser `($ppl$xlen)*1.` `($ppl$ylen)*0.9` 1,0,0.15 @p2Model
