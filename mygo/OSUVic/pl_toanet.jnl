!***********************************************************************
! Description: plots top-of-atmosphere net radiation for 
! PUMA versus ERBE observations
!
! use: go pl_toanet 1 l=1:12@ave "My Model"
!
! arguments  : $1 : data set e.g. 1 or model.output.nc
!            : $2 : time (averaging) period e.g. l=1:12@ave or l=2
!            : $3 : model acromym (optional)
!
!***********************************************************************

def symbol dataset = $1%1%
def symbol tim = $2%l=1%
def sym model = $3%MODEL%
def sym lev = lev=(-180)(-140)(-100,100,10)(140)(180)
def sym lev2 = lev=(-180)(-140)(-100,100,20)(140)(180)
def sym lev3 = lev=(-100,100,10)
def sym lev4 = lev=(-100,100,40)
go portraitNxN 2 3 95 30
!set win/asp=0.9
set reg/x=180:540
!set axis/modulo xt
ppl axlsze .14,.14
ppl labset .18,.16,.16,.16
ppl shakey ,,.12,,,,,,
pal centered
pal blue_darkred
let toanetm = tsr[($2),d=($dataset)]+ttr[($2),d=($dataset)]
def grid/like=tsr[d=($dataset)] mg

use "/home/aschmitt/data/erbe/ERBE_mean.cdf"
let toaneto = net[g=mg@ave]

set view 1
shade/set/nolab/($lev) toaneto
ppl shakey 1,1,0.16
ppl shade
fill/nolab/o/($lev) toaneto
con/nolab/o/($lev2) toaneto
go land
label/nouser `($ppl$xlen)/2` `($ppl$ylen)*1.1` 0,0,0.2 "TOA Net Radiation (W/m^2)"
label/nouser `($ppl$xlen)*0.75` `($ppl$ylen)*0.85` 0,0,0.2 "ERBE"
label/nouser `($ppl$xlen)*0.75` `($ppl$ylen)*0.8` 0,0,0.2 "(1985-1989)"

set view 3
shade/set/nolab/($lev) toanetm
ppl shakey 1,1,0.16
ppl shade
fill/nolab/o/($lev) toanetm
con/nolab/o/($lev2) toanetm
go land
label/nouser `($ppl$xlen)*0.7` `($ppl$ylen)*0.8` -1,0,0.2 ($model)

set view 5
let delta = toanetm - toaneto
shade/set/nolab/($lev3)/pal=light_centered delta
ppl shakey 1,1,0.16
ppl shade
fill/nolab/o/($lev3)/pal=light_centered delta
con/nolab/o/($lev4)/pal=light_centered delta
go land
label/nouser `($ppl$xlen)*0.95` `($ppl$ylen)*0.9` 1,0,0.2 ($model)-ERBE
