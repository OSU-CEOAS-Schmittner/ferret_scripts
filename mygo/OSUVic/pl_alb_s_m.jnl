!***********************************************************************
! Description: plots surface albedo
! PUMA versus ERBE observations
!
! use: go pl_toanet 1 l=1:12@ave "My Model"
!
! arguments  : $1 : data set e.g. 1 or model.output.nc
!            : $2 : month(s) e.g. l=1:12@ave or l=2
!            : $3 : model time (averaging) period e.g. l=1:12@ave or l=2
!            : $4 : model acromym (optional)
!
!***********************************************************************

def symbol dataset = $1%1%
def symbol otim = $2%l=1%
def symbol mtim = $3%l=1%
def sym model = $4%MODEL%
def sym lev = lev=(0,1,.1)
def sym lev2 = lev=(-inf)(-.3,.3,.05)(inf)
go portraitNxN 2 2 5 20
set win/asp=0.8
set reg/x=180:540
!set axis/modulo xt
ppl axlsze .14,.14
ppl shakey ,,.12,,,,,,
pal default


use climatological_axes
ca da climatological_axes

use "/home/aschmitt/data/erbe/albedo_sfc.cdf"
def grid/like=tsr[d=($dataset)]/t=albedo mg
let obs0 = albedo[d=albedo_sfc]/100

ca var alb
let alb0 = if ssr gt 1 then alb
let mod0 = alb0[d=($dataset)]
def grid/like=mod0/t=obs0 mg

let obs = obs0[g=mg@ave,($otim)]
let mod1 = mod0[($mtim)]
let mod2 = mod1[gt=month_reg@mod]
let mod = mod2[($otim)]

set view 1
shade/set/nolab/($lev) obs
ppl axlsze 0.,.14
ppl shakey 1,1,0.16
ppl shade
fill/nolab/o/($lev) obs
con/nolab/o/($lev) obs
go land
label/nouser `($ppl$xlen)*1.2` `($ppl$ylen)*1.1` 0,0,0.2 "Surface Albedo"
label/nouser `($ppl$xlen)` `($ppl$ylen)*0.95` 1,0,0.15 "ERBE 1985-1989"

set view 2
shade/set/nolab/($lev)/nokey mod
ppl axlsze 0.14,0.
ppl shade
fill/nolab/o/($lev) mod
con/nolab/o/($lev) mod
go land
label/nouser `($ppl$xlen)` `($ppl$ylen)*0.95` 1,0,0.15 ($model)

set view 3
let delta = mod - obs
shade/set/nolab/($lev2)/pal=light_centered delta
ppl axlsze 0.14,0.14
ppl shakey 1,1,0.16
ppl shade
fill/nolab/o/($lev2)/pal=light_centered delta
go land
label/nouser `($ppl$xlen)*1.` `($ppl$ylen)*0.95` 1,0,0.15 MODEL-ERBE

set view 4
pl/nolab/axes=0,1,0,1 obs[x=@ave], mod[x=@ave]
label/nouser `($ppl$xlen)*1.` `($ppl$ylen)*0.95` 1,0,0.15 ERBE
label/nouser `($ppl$xlen)*1.` `($ppl$ylen)*0.9` 1,0,0.15 @p2MODEL
