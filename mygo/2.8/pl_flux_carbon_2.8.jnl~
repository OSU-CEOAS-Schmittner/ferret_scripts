!***********************************************************************
! Description: plots sea-air CO2 flux for Model vs. Takahashi
! observations
!
! use: go pl_flux_carbon l=1:12@ave 1 "My Model"
!
! arguments  : $1 : time (averaging) period e.g. l=1:12@ave or l=2
!            : $2 : data set e.g. 1 or model.output.nc
!            : $3 : model acromym
!
!***********************************************************************

def symbol dataset = $1%1%
def symbol tim = $2%l=1%
def sym model = $3%MODEL%
def sym lev = lev=(-inf)(-5,5,1)(inf)
go portraitNxN 2 3 60 24
!set win/asp=0.9
set reg/x=120:480
set axis/modulo xt
ppl axlsze .14,.14
ppl labset .18,.16,.16,.16
ppl shakey ,,.12,,,,,,
pal centered
pal blue_darkred
let gsss = salinity[x=0:360@ave,y=90s:90n@ave,k=1]
let gsdic = dic[x=0:360@ave,y=90s:90n@ave,k=1]

let virtual_flux = FLUX_SALT/gsss*gsdic
let flux_carbon = flux_dic-virtual_flux

set view 1
use "/raid1/aschmitt/data/takahashi/fluxdata.cdf"
let co2_flux_mg = CO2_FLUX[gx=flux_carbon[d=1],gy=flux_carbon[d=1]]
shade/set/nolab/($lev) co2_flux_mg[l=1:12@ave]
ppl shakey 1,1,0.16
ppl shade
fill/nolab/o/($lev) co2_flux_mg[l=1:12@ave]
sha/o/nolab/lev=(0)/pal=grey kmt[d=($dataset)]
label/nouser `($ppl$xlen)/2` `($ppl$ylen)*1.1` 0,0,0.2 "Sea-Air CO_2 Flux (mol m^-^2 yr^-^1)"
label/nouser `($ppl$xlen)*0.7` `($ppl$ylen)*0.8` -1,0,0.2 "Obs 1995"

set view 3
let year = 3600*24*365
let fac = year*(-1e-2)
let fac = year*(-1)
let flx_c = flux_carbon[d=($dataset),($tim)]*fac
shade/set/nolab/($lev) flx_c
ppl shakey 1,1,0.16
ppl shade
fill/nolab/o/($lev) flx_c
sha/o/nolab/lev=(0)/pal=grey kmt[d=($dataset)]
label/nouser `($ppl$xlen)*0.7` `($ppl$ylen)*0.8` -1,0,0.2 ($model)

set view 5
pl/set/nolab/li=1/thick co2_flux_mg[l=1:12@ave,x=@ave,d=fluxdata]
ppl AXLSZE 0.16 0.16
ppl LABSET,,0.2,0.2,
!ppl ylab Sea-Air CO_2 Flux (mol m^-^2 yr^-^1)
ppl yfor (f4.1)
ppl plot
pl/nolab/o/li/col=red/thick=3 flux_carbon[($tim),d=($dataset),x=@ave]*fac
pl/o/nolab/vs/li=1/thick=3 {-10,0,10},{30,30,30}; label -40 30  -1,0,0.2 "WOA01"
pl/o/nolab/vs/li/col=red/thick=3 {-10,10},{27,27}; label -40 27  -1,0,0.2 "MODEL"

let p = co2_flux_mg[l=1:12@ave,d=fluxdata]
let q = flx_c
go regressxy
