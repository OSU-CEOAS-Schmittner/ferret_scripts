! compares simulated CFC11 profiles horizontally averaged in each basin
! use: yes? use mydata.nc; set reg/l=10; go pl_cfc11_xyav.jnl
def symbol dataset = $1%1%
def symbol tim = $2%l=@ave%

go portraitNxN 4 5 45 55
let c11 = cfc11[d=($dataset),($tim)]*1e12/1024.5 ! convert from mol/m-3 to pmol/kg
go set_ocean c11

use "/raid1/aschmitt/data/glodap/CFC.nc"
set axis/depth zt
set axis/modulo xt
define grid/like=cfc11[d=($dataset)]/t=cfc11 mg
let cfc11_mg = cfc11[g=mg]
go set_ocean cfc11_mg mskhr[d=($dataset)]

ppl AXLSZE 0.16 0.16
!ppl AXNMTC,1,0
set view 1
plot/axes=1,0,1,0/nolab/hlim=0:.5:.1 cfc11_mg[x=@ave,y=@ave,d=CFC]
plot/ov/nolab c11[x=@ave,y=@ave]
label/nouser `($ppl$xlen)*0.15` `($ppl$ylen)*.1` -1,0,0.2 "Global"
set view 2
plot/axes=1,0,0,0/nolab/hlim=0.5:4.5:.5 cfc11_mg[x=@ave,y=@ave,d=CFC]
plot/ov/nolab c11[x=@ave,y=@ave]

go legline_zdown 1.2, .5, 1500, 1, .2, "Observations"
go legline_zdown 1.2, .5, 2500, 2, .2, "Model"

set view 5
set reg/y=40s:90n
ppl AXLSZE 0.0 0.16
plot/axes=1,0,1,0/nolab/hlim=0:.5:.1 cfc11_mg_a[x=@ave,y=@ave,d=CFC]
plot/ov/nolab c11_a[x=@ave,y=@ave]
label/nouser `($ppl$xlen)*0.15` `($ppl$ylen)*.1` -1,0,0.2 "Atlantic"
set view 6
plot/axes=1,0,0,0/nolab/hlim=0.5:4.5:.5 cfc11_mg_a[x=@ave,y=@ave,d=CFC]
plot/ov/nolab c11_a[x=@ave,y=@ave]

set view 9
plot/axes=1,0,1,0/nolab/hlim=0:.5:.1 cfc11_mg_i[x=@ave,y=@ave,d=CFC]
plot/ov/nolab c11_i[x=@ave,y=@ave]
label/nouser `($ppl$xlen)*0.15` `($ppl$ylen)*.1` -1,0,0.2 "Indian"
set view 10
plot/axes=1,0,0,0/nolab/hlim=0.5:4.5:.5 cfc11_mg_i[x=@ave,y=@ave,d=CFC]
plot/ov/nolab c11_i[x=@ave,y=@ave]

set view 13
plot/axes=1,0,1,0/nolab/hlim=0:.5:.1 cfc11_mg_p[x=@ave,y=@ave,d=CFC]
plot/ov/nolab c11_p[x=@ave,y=@ave]
label/nouser `($ppl$xlen)*0.15` `($ppl$ylen)*.1` -1,0,0.2 "Pacific"
set view 14
plot/axes=1,0,0,0/nolab/hlim=0.5:4.5:.5 cfc11_mg_p[x=@ave,y=@ave,d=CFC]
plot/ov/nolab c11_p[x=@ave,y=@ave]

ppl AXLSZE 0.16 0.16
set view 17
can reg/y
plot/axes=1,1,1,0/nolab/hlim=0:.5:.1 cfc11_mg_s[x=@ave,y=@ave,d=CFC]
plot/ov/nolab c11_s[x=@ave,y=@ave]
label/nouser `($ppl$xlen)*0.15` `($ppl$ylen)*.1` -1,0,0.2 "Southern Ocean"
set view 18
plot/axes=1,1,0,0/nolab/hlim=0.5:4.5:.5 cfc11_mg_s[x=@ave,y=@ave,d=CFC]
plot/ov/nolab c11_s[x=@ave,y=@ave]

label/nouser `($ppl$xlen)*.5` `($ppl$ylen)*(-0.3)`  0,0,0.2 "CFC11 (pmol/kg)"

let p = cfc11_mg[d=CFC]
let q = c11
go regressxyz




