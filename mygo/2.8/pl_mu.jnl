!***********************************************************************
! Description: plots annual mean phytoplankton growth rate for Model vs. 
! observations from Westberry et al. 
!
! use: go pl_mu l=1:12@ave 1 "My Model"
!
! arguments  : $1 : data set e.g. 1 or model.output.nc
!            : $2 : time (averaging) period obs e.g. l=1:12@ave or l=2
!            : $3 : time (averaging) period model
!            : $4 : model acromym
!
!***********************************************************************

def sym dataset = $1%1%
def sym tobs = $2%l=1%
def sym tmod = $3%l=1%
def sym model = $4%MODEL%
def sym lev = lev=(0,1.5,0.1)(inf)
go portraitNxN 2 3 90 30
!set win/asp=0.9
set reg/x=120:480
set axis/modulo xt
ppl axlsze .14,.14
ppl labset .18,.16,.16,.16
ppl shakey ,,.12,,,,,,
pal centered
pal blue_darkred
pal default

use "/home/aschmitt/data/westberry07gbc/mu_50m_mo_clim.nc"

use ($dataset)
set reg/x=30:390

def grid/x=phyt[d=($dataset)]/y=phyt[d=($dataset)] mg
! ln(2) converts from [divisions per day] to [per day]
let mumg0 = mu[d=mu_50m_mo_clim.nc,g=mg@ave]*ln(2)

let secperday = 3600*24
let u_t = secperday*(onpp+npp_D)/max(phyt+diaz,0) ! total model growth rate [1/d]

let mumg = mumg0+q*0 ! masking
let mumg = mumg0 ! masking
let p = mumg[($tobs)]
let q = u_t[k=1,d=($dataset),($tmod)]


set view 1
shade/set/nolab/($lev) p
ppl shakey 1,1,0.16
ppl shade
fill/nolab/o/($lev) p
sha/o/nolab/lev=(0)/pal=grey kmt[d=($dataset)]
label/nouser `($ppl$xlen)/2` `($ppl$ylen)*1.1` 0,0,0.2 "Phytoplankton growth rate (d^-^1)"
label/nouser `($ppl$xlen)*0.03` `($ppl$ylen)*0.75` -1,0,0.2 "OBS"

set view 3

shade/set/nolab/($lev) q
ppl shakey 1,1,0.16
ppl AXLSZE 0. 0.16
ppl shade
fill/nolab/o/($lev) q
sha/o/nolab/lev=(0)/pal=grey kmt[d=($dataset)]
label/nouser `($ppl$xlen)*0.03` `($ppl$ylen)*0.75` -1,0,0.2 ($model)

set view 5
pl/set/nolab/vlim=0:1.2/thick=3 p[x=@ave]
ppl AXLSZE 0.16 0.16
ppl LABSET,,0.2,0.2,
!ppl ylab Sea-Air CO_2 Flux (mol m^-^2 yr^-^1)
ppl yfor (f4.1)
ppl plot
pl/nolab/o/li/col=red/thick=3 q[x=@ave]
pl/o/nolab/vs/thick=3/li/col=black {-60,-40},{1.1,1.1}; label -85 1.1  -1,0,0.2 "OBS"
pl/o/nolab/vs/li/col=red/thick=3 {-60,-40},{1,1}; label -85 1  -1,0,0.2 "MODEL"

can reg/x

go regressxy
