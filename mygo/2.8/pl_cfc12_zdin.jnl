! compares simulated CFC12 inventory (vertically integrted) 
! with observations (e.g. http://gecko.rsmas.miami.edu/pubs/WilleyCFC.pdf)
! use: set data mydata.nc; set reg/l=4; go pl_cfc12_zdin (mydata) (l=3:4@ave)
def sym dataset = $1%1%
def sym tim = $2%l=@ave%

go portraitNxN 2 2 99 20
let RHO = 1024.5 ! value used in co2calc.f
let/title="CFC12"/units="mol/m^3" c12 = cfc12[d=CFC]*rho/1e12 !convert from pmol/kg to mol/m^3 
set reg/x=120:480
use "/raid1/aschmitt/data/glodap/CFC.nc"
set axis/depth zt
set axis/modulo xt
define grid/like=cfc12[d=1]/t=cfc12 mg
let cfc12_mg = c12[g=mg]
go set_ocean cfc12_mg mskhr[d=1]

!pal inverse_greyscale
def sym opt=lev=(-inf)(0.25)(0.5,2.5,.25)(3.5)(inf)
!def sym opt2=lev=(-inf)(0.25)(0.5,2.5,.25)(3.5)(inf)
def sym opt2=lev=(-inf)(0.5,2.5,.5)(inf)

set view 1
ppl AXLSZE 0.0 0.16
ppl SHAKEY 1, 1, 0.15, 1, 4, 8, 9.4, 10.2, 1.4, 7.4
fill/nolab/($opt2) cfc12_mg[z=@din]*1e6
con/ov/nolab/set/($opt) cfc12_mg[z=@din]*1e6
ppl conset 0.16,1,,,,,,2.5
ppl contour/ov
label/nouser `($ppl$xlen)/2` `($ppl$ylen)*1.1` 0,0,0.2 "CFC12 Inventory (mol/km^2)"
label/nouser `($ppl$xlen)*.8` `($ppl$ylen)*.8` 0,0,0.2 "Observations"

use ($dataset)
set view 3
ppl AXLSZE 0.16 0.16
ppl SHAKEY 1, 1, 0.15, 1, 4, 8, 9.4, 10.2, 1.4, 7.4
fill/nolab/($opt2) cfc12[z=@din,($tim)]*1e6
con/ov/nolab/set/nokey/($opt) cfc12[z=@din,($tim)]*1e6
ppl conset 0.16,1,1,,,,,2.5
ppl contour/ov
label/nouser `($ppl$xlen)*.8` `($ppl$ylen)*.8` 0,0,0.2 "Model"

